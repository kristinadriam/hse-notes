## Строгие и нестрогие функции. Ленивое и энергичное исполнение. Форсирование, слабая головная нормальная форма.

### Значение незавершающегося вычисления
Сколько значений у типа `Bool`? На первый взгляд два — `True` и `False`, в соответствии с определением:

```haskell
data Bool = True | False
```

Но это не так! Вспомним выражение `bot :: Bool`, определённое рекурсивно

```haskell
bot :: Bool
bot = not bot
```

Его значение — не `True` и не `False`, это не завершающееся вычисление. В статической семантике языка Haskell ему приписывается значение `⊥` (напомним, что этот символ читается как основание или дно).

Ничего специфически булева в `⊥` нет, можно написать расходящиеся вычисления для любого типа. ⊥ — значение, разделяемое всеми типами:

```haskell
⊥ :: forall {a}. a
```

Ошибкам, приводящим к аварийному завершению программы, тоже приписывается это
значение. Аварийное прерывание может произойти в вычислении любого типа, поэтому

```haskell
ghci> :t undefined
undefined :: forall {a}. a
```

Эта «наивысшая степень полиморфизма» `undefined` приводит к следующему интересному наблюдению: если какое-то подвыражение проходящего проверку типов выражения заменить на `undefined`, то новое выражение тоже пройдет проверку типов. Поэтому `undefined` часто используют вместо еще не написанных частей программы.

### Нестрогая (ленивая) семантика
При вычислениях в рамках этой модели сокращается внешний редекс. Это приводит к тому, что аргументы передаются в функцию невычисленными. Например, для

```haskell
ignore x = 42
```

следующие вызовы не расходится

```haskell
ghci> ignore bot
42
ghci> ignore undefined
42
```

Такие функции как `ignore`, игнорирующие значение своего аргумента, называются _нестрогими (non-strict)_ по этому аргументу. _Строгая (strict)_ функция, наоборот, определяется как расходящаяся на расходящемся аргументе, то есть ее характеристическое свойство

```haskell
f⊥=⊥
```

Классификация по шкале строгий-нестрогий связана с тем, как функция реагирует на переданную в качестве аргумента расходимость. Есть еще два важных понятия, описывающих может ли функция порождать расходимость или нет. Если функция завершается при всех нерасходящихся значениях аргументов, ее называют _тотальной (total)_. Если же имеются нерасходящиеся значения аргументов, на которых функция расходится, то ее называют _частичной (partial)_.

### Форсирование вычислений
В Haskell имеется возможность локально перейти к энергичной модели вычислений или, иными словами, форсировать вычисления по требованию программиста. Для этого используют специальный комбинатор `seq :: a -> b -> b`. Он может быть описан следующей парой уравнений

```haskell
seq ⊥ b = ⊥
seq a b = b, если a /= ⊥
```

Отметим, что это описание поведения, а не реализация через механизм сопоставления с образцом (`⊥` не является образцом). Это встроенный комбинатор, позволяющий нарушить ленивую семантику языка. Вычисление его первого аргумента происходит без необходимости, форсируется.

Использование `seq` потворствует распространению `⊥`, интересуясь значением своего первого аргумента

```haskell
ghci> seq undefined 42
*** Exception: Prelude.undefined
ghci> seq (id undefined) 42
*** Exception: Prelude.undefined
```

Однако `seq` не форсирует вычисления для всех входящих в выражение редексов. Если у нас на верхнем уровне находится конструкция, отличная от редекса, то `seq` останавливается. К таким конструкциям относятся конструкторы данных, лямбда-абстракции и частично примененные функции. Эти конструкции, являясь «значениями», обеспечивают барьер для распространения `⊥`

```haskell
ghci> seq (undefined,undefined) 42
42
ghci> seq (\x -> undefined) 42
42
ghci> seq ((+) undefined) 42
42
```

Иногда упомянутые выше «не редексы» (конструкторы данных, лямбда-абстракции и частично примененные функции) объединяют одним термином – их называют _слабой головной нормальной формой (weak head normal form, WHNF)_. Вводя это определение, можно перейти к короткой формулировке: `seq` форсирует вычисления до WHNF.

### Использование seq
Через seq определяется оператор энергичного применения (с вызовом-по-значению)

```haskell
infixr 0 $!
($!) :: (a -> b) -> a -> b
f $! x = x `seq` f x
```

Как уже отмечалось ранее переход к энергичной модели приводит к «худшей определенности»

```haskell
ghci> ignore undefined
42
ghci> ignore $! undefined
*** Exception: Prelude.undefined
```

Приведем пример использования энергичного применения. Вспомним факториал c аккумулирующим параметром

```haskell
factorial n = helper 1 n where
  helper acc k | k > 1 = helper (acc * k) (k - 1)
               | otherwise = acc
```

Из-за ленивости к моменту срабатывания `otherwise` параметр `acc` будет содержать недовычисленое выражение вида

```haskell
(...((1 * n) * (n - 1)) * (n - 2) * ... * 2)

```

Оптимизатор GHC обычно справляется, имея встроенный _анализатор строгости_. Но
можно, не полагаясь на него, написать

```haskell
factorial n = helper 1 n where
  helper acc k | k > 1 = (helper $! acc * k) (k - 1)
               | otherwise = acc
```
