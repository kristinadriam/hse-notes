## 14. Класс типов Foldable и его представители

Минимальное полное определение: `foldMap` или `foldr`.

```haskell
class Foldable t where
    fold :: Monoid m => t m -> m
    fold = foldMap id
    foldMap :: Monoid m => (a -> m) -> t a -> m
    foldMap f = foldr (mappend . f) mempty
    foldr, foldr' :: (a -> b -> b) -> b -> t a -> b
    foldr f z t = appEndo (foldMap (Endo . f) t) z
    foldl, foldl' :: (a -> b -> a) -> a -> t b -> a
    foldl f z t = appEndo (getDual (foldMap (Dual . Endo . flip f) t)) z
    foldr1, foldl1 :: (a -> a -> a) -> t a -> a
```

Примеры реализации для простейших контейнеров:

1) Список

    ```haskell
    instance Foldable [] where
        foldr = Data.List.foldr
        foldl = Data.List.foldl
        foldr1 = Data.List.foldr1
        foldl1 = Data.List.foldl1
    ```

2) `Maybe`

    ```haskell
    instance Foldable Maybe where
        foldr _ z Nothing = z
        foldr f z (Just x) = f x z
        foldl _ z Nothing = z
        foldl f z (Just x) = f z x
    ```

3) Пара

    ```haskell
    instance Foldable ((,) a) where
        foldMap f (_, y) = f y 
        foldr f z (_, y) = f y z
        length _ = 1
        null _ = False
    ```

4) `Either`

    ```haskell
    instance Foldable (Either a) where
        foldMap _ (Left _) = mempty
        foldMap f (Right y) = f y
        foldr _ z (Left _) = z
        foldr f z (Right y) = f y z
        length (Left _) = 0
        length (Right _) = 1
        null = isLeft
    ```

Также мы можем выполнять свертку нелинейных типов данных. Например, собственной структуры дерево. Она будет различна в зависимости от того, в каком порядке мы хотим обойти дерево.

Если в контейнере лежит элемент типа `Monoid`, то мы можем для него определить функцию `fold`, которая в качестве инициализирующего значения использует нейтральный элемент, а в качестве функции бинарную операцию, которая для этого типа определена. (В классе моноид определена похожая функция, которая называется `mconcat`).

Представителями класса `Foldable` являются список, `Maybe`, а также `Set` из Data.Set, Map k из Data.Map, Seq из Data.Sequence, Tree из Data.Tree и т.п.

#### Полное определение класса `Foldable`

```haskell
class Foldable t where
    ...
    toList :: t a -> [a]
    null :: t a -> Bool
    null = foldr (\_ _ -> False) True
    length :: t a -> Int
    length = foldl' (\c _ -> c + 1) 0
    elem :: Eq a => a -> t a -> Bool
    sum, product :: Num a => t a -> a
    sum = getSum . foldMap Sum
    maximum, minimum :: Ord a => t a -> a
```

#### Законы `Foldable`

```haskell
foldr f z t ≡ appEndo (foldMap (Endo . f) t ) z
foldl f z t ≡ appEndo (getDual (foldMap (Dual . Endo . flip f) t)) z
fold ≡ foldMap id
length ≡ getSum . foldMap (Sum . const 1)
sum ≡ getSum . foldMap Sum
product ≡ getProduct . foldMap Product
minimum ≡ getMin . foldMap Min
maximum ≡ getMax . foldMap Max
foldr f z ≡ foldr f z . toList
foldl f z ≡ foldl f z . toList
```

Если контейнер `t` не только `Foldable`, но и `Functor`, то

```haskell
foldMap f ≡ fold . fmap f
foldMap f . fmap g ≡ foldMap (f . g)
```

Второе следует из первого благодаря закону `Functor`.

Многие функции, исторически реализованные для списков, были обобщены до `Foldable`:

```haskell
concat :: Foldable t => t [a] -> [a]

concatMap :: Foldable t => (a -> [b]) -> t a -> [b]

and,or :: Foldable t => t Bool -> Bool

any,all :: Foldable t => (a -> Bool) -> t a -> Bool

maximumBy,minimumBy :: Foldable t => (a -> a -> Ordering) -> t a -> a

notElem :: (Foldable t, Eq a) => a -> t a -> Bool

find :: Foldable t => (a -> Bool) -> t a -> Maybe a
```

