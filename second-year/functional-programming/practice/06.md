## Алгебраические типы данных. Сопоставление с образцом, его семантика. Полиморфные и рекурсивные типы данных

Вспомним введенные ранее понятия образца (pattern) и сопоставления с образцом (pattern matching). Рассмотрим, например, библиотечную функцию, переставляющую элементы пары

```haskell
swap :: (a,b) -> (b,a) -- Data.Tuple 
swap (x,y) = (y,x)
```

Конструкция `(x,y)` в левой части определения функции представляет собой образец. Синтаксически он практически идентичен конструктору данных `(y,x)` в правой части. Действительно, конструкторы данных — это основной строительный блок для образцов. Однако, если применение конструкторов по прямому назначению конструирует данные, в образцах конструкторы используются для деконструкции переданных аргументов и извлечения их структурных составных частей.

При вызове

```haskell
ghci> swap (5+2,True)
(True,7)
```

происходит сопоставление с образцом:
• проверяется, что конструктор `(,)` — подходящий (для пары это тривиально);
• переменные `x` и `y` связываются с выражениями `5+2` и `True`;
• осуществляется подстановка выражений вместо переменных в теле функции `swap`.

До сих пор мы в основном имели дело со встроенными типами данных. Пользовательские типы данных в Haskell строятся из базовых с помощью трех операциий над типами: суммы, произведения и возведения в степень.

#### Тип суммы

Типы данных подобные

```haskell
data Bool = False | True
```

называются перечислениями. Они представляют собой набор конструкторов, соединенных оператором `|`. Для того, чтобы сконструировать значение перечисления нужно выбрать один конструктор, поэтому оператор `|` читается как «или». Типы построенные с помощью этого оператора называют типами суммы.

В Prelude, помимо Bool, доступно еще одно полезное перечисление c тремя конструкторами данных

```haskell
data Ordering = LT | EQ | GT
```

Конечно же мы можем объявлять пользовательские типы данных самостоятельно, используя ключевое слово data. Вот пример пользовательского перечисления с четырьмя конструкторами данных

```haskell
data CardinalDirection = North | East | South | West 
```

Конструкторы данных имеют тип CardinalDirection:

```haskell
ghci> dir = North
ghci> :t dir
dir :: CardinalDirection
```

Однако попытка вывести значение dir приведет к ошибке:

```haskell
ghci> dir
 error: No instance for (Show CardinalDirection)
        arising from a use of `print'
    In a stmt of an interactive ghci command: print it
```

Дело в том, что в поток вывода выводится строковое представление для значения типа данных, а как оно должно быть организовано зависит от намерений программиста. За перевод типа данных в строковое представление отвечает класс типов `Show`. Если мы хотим обеспечить такой перевод для пользовательского типа данных `CardinalDirection`, необходимо реализовать представителя класс типов Show для нашего типа. Однако, если нас устраивает простой вывод имени конструктора, можно попросить компилятор сгенерировать этого представителя автоматически, используя механизм _производных представителей (deriving instances)_. Это очень просто сделать, достаточно внести небольшие изменения в объявление типа

```haskell
data CardinalDirection = North | East | South | West deriving Show
```

Механизм производных представителей работает для целого ряда библиотечных классов типов.
Поскольку перечисления (и, шире, типы суммы) имеют несколько конструкторов, сопоставление с образцом должно порождать ветвление. Оно достигается определением функции через набор равенств — по равенству на каждый интересный в контексте реализации данной функции образец.

```haskell
hasPole :: CardinalDirection -> Bool
hasPole North = True
hasPole South = True
hasPole _     = False
```

Сопоставление с образцом происходит сверху вниз, до первого удачного

```haskell
ghci> hasPole South
True
ghci> hasPole West
False
```

Отметим, что порядок перечисления образцов важен — если в реализации `hasPole` переместить последнее уравнение вверх, результаты будут другими.

Встроенные типы данных ведут себя так, как будто они определены как перечисления:

```haskell
data Char = '\NUL' | ... | 'a' | 'b' | 'c' | 'd' | ...
          | '\1114111'
data Int = -9223372036854775808 | ...
         | -2 | -1 | 0 | 1 | 2 | ...
         | 9223372036854775807
data Integer = ... | -2 | -1 | 0 | 1 | 2 | ...
```

Это позволяет использовать соответствующие литералы как образцы

```haskell
isAnswer :: Integer -> Bool
isAnswer 42 = True
isAnswer _  = False
```

#### Семантика сопоставления с образцом

Поскольку определение функции может содержать

- несколько равенств;
- несколько аргументов-образцов в каждом равенстве;
- вложенные подобразцы в каждом образце требуются объемные правила сопоставления.

Общий принцип, однако, описать довольно просто: __сопоставление с образцом происходит сверху-вниз, затем слева- направо.__ Результат сопоставления с образцом может быть

- успешным (succeed);
- неудачным (fail);
- расходящимся (diverge).

Успех означает, что мы можем переходить к правой части текущего равенства. Разница между неудачей и расходимостью такова: при неудаче мы просто переходим к следующему равенства, а расходимость аварийно завершает работу.
Образцам помимо конструкторов называют также переменные и подчеркивание. Последние два вида образцов относят к _неопровержимым (irrefutable)_. Неопровержимы те образцы, сопоставление с которыми всегда успешно. Образец, сопоставление с которым может завершиться отличным от успеха исходом называют _опровержимым (refutable)_.

Посмотрим как работают приведенные правила на примере функции

```haskell
bar 1 2 = 3
bar 0 _ = 5
```

Вызов `bar 1 2` приводит к успеху на первом же равенстве. Вызов `bar 0 7` неудачен на первом равенстве, и успешен на втором. Вызов`bar 2 1` — две неудачи и, как следствие, расходимость.

Вызов `bar 1 (5-3)` будет успешен на первом равенстве. Однако для того, чтобы удостовериться в этом, системе времени исполнения придется форсировать вычисление аргумента. Это добавляет в ленивый язык некоторую (необходимую) степень энергич- ности. Однако вычисление форсируется только до слабой головной нормальной формы (WHNF): достаточно получить на верхнем уровне некоторый конструктор данных. Все, что находится за его барьером, остается не вычисленным.

Вызов `bar 1 undefined` приводит к расходимости в первом же образце, причина этого ясна из предыдущего абзаца.

Однако вызов `bar 0 undefined` возвращает 5, то есть он неудачен (но не расходится!) в первом образце и успешен во втором. Здесь срабатывает правило «слева направо»: в первом равенстве неудача сопоставления фактического 0 с ожидаемой 1 приводит к тому, что все сопоставление объявляется неудачным, дело просто не доходит до потенциальной расходимости.

#### Тип произведения

Декартово _произведение_ нескольких типов — это тип, в котором лежат значения всех сомножителей. Простейшие произведения — это встроенные кортежи (пары, тройки и т.д.). Однако работа с кортежами не очень удобна, часто хочется иметь конструктор данных, название которого отражает логику предметной области. Пусть мы, например, хотим работать с точками на координатной плоскости, и использовать встроенный тип `Double` для хранения координат. В Haskell для этих целей мы можем использовать тип- произведение двух значений типа `Double` с одним именованным конструктором данных:

```haskell
data PointDouble = PtD Double Double
  deriving Show
```

Отметим, что имя типа данных (в нашем примере `PointDouble`) может не совпадать с именем конструктора данных (в нашем примере `PtD`), хотя в случае, когда конструктор данных один, их обычно делают одинаковыми.

Конструктор данных имеет тип функции, арность которой равна количеству сомно- жителей тип-произведения. Как и любая функция, конструктор может применяться частично

```haskell
ghci> :t PtD
PtD :: Double -> Double -> PointDouble
ghci> :t PtD 3
PtD 3 :: Double -> PointDouble
ghci> :t PtD 3 5
PtD 3 5 :: PointDouble
```

Однако конструктор данных, в отличие от функции, не имеет никакого кодируемого пользователем вычислительного поведения. Будучи снабженным требуемым количе- ством фактических аргументов, он просто порождает значение целевого типа, высту- пающее в роли хранилища для выражений-аргументов.

Сопоставление с образцом для типа-произведения решает обратную задачу — из- влечение выражений из этого хранилища и связывания их с именами для дальнейшего использования. Вот, например, функция, которая по переданным координатам двух точек ищет координаты середины соединяющего эти точки отрезка:

```haskell
midPointDouble :: PointDouble -> PointDouble -> PointDouble
midPointDouble (PtD x1 y1) (PtD x2 y2) =
   PtD ((x1 + x2) / 2) ((y1 + y2) / 2)
```

Координаты точек-аргументов функции связываются в образце с переменными и обра- батываются в правой части для получения координат середины отрезка:

```haskell
ghci> midPointDouble (PtD 3 5) (PtD 9 8)
PtD 6.0 6.5
```

### Полиморфные типы

Тип данных `PointDouble` был создан в представлении, что координаты точки имеют тип `Double`. Однако могут возникнуть ситуации, когда удобнее иметь координаты типа `Float` или вообще целочисленные, например, `Int`.

Универсальным решением будет сделать тип точки полиморфным по типу координат. Для этого нужно параметризовать тип точки ти повым параметром:

```haskell
data Point a = Pt a a
  deriving Show
```

Теперь тип конструктора данных полиморфен

```haskell
ghci> :type Pt
Pt :: a -> a -> Point a
```

и мы можем создавать точки с разными типами координат

```haskell
ghci> pDbl = Pt (3::Double) (5::Double)
ghci> pDbl
Pt 3.0 5.0
ghci> pInt = Pt (3::Int) (5::Int)
ghci> pInt
Pt 3 5
ghci> :t pDbl
pDbl :: Point Double
ghci> :t pInt
pInt :: Point Int
```

Сам по себе конструктор типа `Point` становится теперь не обычным типом, а функцией над типами (чаще говорят _оператор над типами_, хотя это не оператор в смысле Haskell). Конкретный тип, напирмер, `Point` Int получается применением конструктора типа к обычному типу; в нашем случае к `Int`. Обратим внимание, что здесь используется тот же минималистичный синтаксис применения, что и при построении выражений, хотя речь идет о построении типов!

Конструктор типа `Point` имеет один типовый параметр `a`. Это значит, что его применения `Point Int` или `Point Double` допустимы, а, например, `Point Int Double` нет. Очевидно, что конструкция `Point Point` тоже должна быть отвергнута: хотя число параметров здесь подходящее, тип параметра не тот что нужен. Постойте-ка, мы заговорили о типе типа! Для статического контроля подобного рода в Haskell имеется система типов над системой типов, которая для избежания путаницы называется системой _кайндов_.

Система кайндов структурно повторяет систему типов Haskell, однако базовый кайнд всего один — `*`. Все простые типы имеют этот кайнд. К простым относятся все типы, которые нами использовались справа в отношении типизации `::`, в том числе и стрелочные. Этот же оператор `::` используется для отношения «кайндизации» в системе кайндов. Ровно так же как в системе типов утверждение `42 :: Int` читалось как «42 — это целое», в системе кайндов утверждение `Int ::*` читается как `«Int` — это (простой) тип». Утверждение `(+5) :: Int -> Int`, читающееся как «`(+5)` — это функция с одним параметром из `Int` в `Int»`, в системе кайндов имеет аналог `Point :: *->*`, который читается как `«Point` — это однопараметрический оператор из простого типа в простой тип».

ghci позволяет анализировать кайнды как пользовательских типов

```haskell
ghci> :kind Point
Point :: *->*
ghci> :kind Point Int
Point Int :: *
```

так и встроенных

```haskell
ghci> :k []
[] :: *->*
ghci> :k [] Int
[] Int :: *
ghci> :k [Int]
[Int] ::*
ghci> :k (,)
(,) :: *->* -> *
ghci> :k (,) Char
(,) Char ::* -> *
ghci> :k (,) Char Bool
(,) Char Bool ::*
ghci> :k (Char,Bool)
(Char,Bool) :: *
ghci> :k Int -> Int -> Int
Int -> Int -> Int ::*
ghci> :k (->)
(->) :: *->* -> *
ghci> :k (->) Int
(->) Int ::* -> *
ghci> :k (->) Int Bool
(->) Int Bool ::*
```

#### Полиморфные функции над полиморфными типами

Полиморфные типы данных полиморфны параметрически, то есть на типовый параметр `a` в типе `Point a` невозможно наложить ad hoc ограничения. Однако при реализации функций над полиморфными типами подобные ограничения допустимы. Например, в общем случае невозможно найти середину отрезка, если координаты вершин целочисленны. Поэтому в полиморфном обобщении подобной функции возникает кон- текст `Fractional`:

```haskell
midPoint :: Fractional a => Point a -> Point a -> Point a
midPoint (Pt x1 y1) (Pt x2 y2) =
  Pt ((x1 + x2) / 2) ((y1 + y2) / 2)
```

Контекст `Fractional` нет необходимости писать руками, он выводится автоматически из использования в теле функции оператора `(/)`. Если бы в теле использовался только оператор сложения, мы бы имели более общий контекст `Num`.

```haskell
ghci> :t midPoint (Pt 3 5) (Pt 9 8)
midPoint (Pt 3 5) (Pt 9 8) :: Fractional a => Point a
ghci> midPoint (Pt 3 5) (Pt 9 8)
Pt 6.0 6.5
```

### Стандартные алгебраические типы

В стандартной библиотеке помимо кортежей и списков имеется несколько полезных полиморфных типов общего назначения.

Тип `Maybe a` позволяет добавить к произвольному типу a дополнительное необязательное значение

```haskell
data  Maybe a  =  Nothing | Just a
```

Конструктор `Just` оборачивает «регулярное» значение, а `Nothing` играет роль дополнительного элемента. Для извлечения значения можно использовать сопоставление с образцом или библиотечную функцию-элиминатор (из модуля Data.Maybe)

```haskell
maybe :: b -> (a -> b) -> Maybe a -> b
```

Эта функция позволяет по переданному значению типа Maybe a получить значение произвольного типа `b`, если у нас имеется обработчик типа `a -> b` для «регулярных» значений и значение типа `b` для случая `Nothing`. Например,

```haskell
ghci> maybe (-1) length $ Just "ABCDE"
5
ghci> maybe (-1) length $ Just ""
0
ghci> maybe (-1) length Nothing
```

Другим примером использования типа `Maybe` может служить библиотечная функция поиска элемента списка, удовлетворяющего предикату:

```haskell
find :: (a -> Bool) -> [a] -> Maybe a
```

Если элемент, удовлетворяющий предикату, отсутствует, возвращается `Nothing`:

```haskell
ghci> find (>10) [7,5,12,16]
Just 12
ghci> find (>20) [7,5,12,16]
Nothing
```

Тип `Either a b` хранит одно значение из двух, являясь типом-суммой двух произвольных типов

```haskell
data Either a b = Left a | Right b
```

Этот тип является стандартным библиотечным способом построить сумму типов, взяв два существующих типа. В этом он похож на стандартный двухэлементный кортеж (пару), которая обеспечивает стандартный способ построить произведение двух типов.

В модуле Data.Either определен стандартный элиминатор для типа Either

```haskell
either :: (a -> c) -> (b -> c) -> Either a b -> c
```

С помощью `Either` обычно описывают вычисления, в которых возможны проблемные результаты. Конструктор данных `Right` используют для упаковки успешных результатов, а `Left` — для реакции на неудачу.

```haskell
(/?) :: (Eq a, Fractional a) => a -> a -> Either String a
x /? 0 = Left "(/?) error: division by zero"
x /? y = Right (x / y)
ghci> 6 /? 2
Right 3.0
ghci> 6 /? 0
Left "(/?) error: division by zero"
ghci> :t either id show
either id show :: Show a => Either String a -> String
ghci> either id show (6 /? 2)
"3.0"
ghci> either id show (6 /? 0)
"(/?) error: division by zero"
```

На основе типа `Either` в Haskell построен стандартный монадический механизм обработки исключений.
И `Maybe` и `Either` представляют собой суммы типов. При этом слагаемые в трех случаях из четырех можно рассматривать как типы-произведения из одного сомножителя, а в четвертом (`Nothing`) — из нуля сомножителей.

### Экспоненциальные типы

_Экспоненциальный тип_ — это тип функции. Такое название происходит из наблюдения за мощностями типов, то есть количеством их элементов. У типа `Bool` мощность равна 2, у типа `Ordering` — 3. У типа их суммы `Either Bool Ordering` мощность равна `2+3 = 5`. У типа их произведения `(Bool,Ordering)` имеется`2 * 3 = 6` различных элементов. Если посчитать число различных функций типа `Bool -> Ordering`, то окажется, что их $3^2 = 9$. Если же посчитать число различных функций типа `Ordering -> Bool`, то окажется, что их $2^3 = 8$. Иными словами, на вполне разумных основаниях вместо стандартного встроенного типа функции `a -> b` можно было бы использовать нотацию `b ^ a`.

Экспонециальный (или функциональный) тип — это полноценный тип языка, его можно использовать всюду, где мы можем писать произвольный тип. В частности, мы можем использовать его в качестве типа аргумента конструктора данных:

```haskell
data Endom a = Endom (a -> a)
```

Этот тип представляет собой контейнер для хранения функции, у которой область определения и множество значений совпадают. Можно написать элиминатор для извлечения функции из такого контейнера:

```haskell
appEndom :: Endom a -> a -> a
appEndom (Endom f) = f
```

Извлечение функции происходит при частичном применении элиминатора

```haskell
ghci> e = Endom (\n -> 2 * n + 3)
ghci> :t e
e :: Num a => Endom a
ghci> :t appEndom e
appEndom e :: Num a => a -> a
```

А полное применение `appEndom` дает извлечение-а-затем-применение

```haskell
ghci> e `appEndom` 5
13
```

У экспоненциального типа есть оно отличие от типов суммы и произведения. Мы не можем «разобрать» такой тип с помощью сопоставления с образцом, переменная является единственным подходящим образцом для функционального аргумента.

### Рекурсивные типы

При объявлении типов можно использовать рекурсию. Это значит, что допустимо указывать объявляемый тип в качестве аргумента конструктора данных:

```haskell
ghci> data Nat = Zero | Suc Nat deriving Show
```

Конструктор `Suc` при этом оказывается эндоморфизмом над типом `Nat`

```haskell
ghci> :t Zero
Zero :: Nat
ghci> :t Suc
Suc :: Nat -> Nat
```

Это позволяет строить неограниченное число обитателей данного типа:

```haskell
ghci> one = Suc Zero
ghci> two = Suc (Suc Zero)
ghci> three = Suc two
ghci> four = Suc three
...
```

Это так называемые числа Пеано. Их можно рассматривать как способ кодирования натуральных чисел в унарной системе исчисления. Поскольку тип `Nat` это тип суммы, тотальные функции над ним, использующие опровержимые образцы, определяются несколькими равенствами

```haskell
ghci> {pred (Suc n) = n; pred Zero = Zero}
ghci> pred two
Suc Zero
```

Другим примером рекурсивного типа служит список. Хотя встроенный список имеет специальный синтаксис, мы можем определить полностью эквивалентный ему пользовательский тип списка

```haskell
data List a = Nil | Cons a (List a)
  deriving Show
```

Конструкторы данных типа `List` имеют типы

```haskell
Nil :: List a
Cons :: a -> List a -> List a
```

Пользовательские функции над рекурсивными типами данных удобно писать через сопоставление с образцом и рекурсию

```haskell
len :: List a -> Int
len Nil = 0 len(Cons_xs) = 1+lenxs
```

Рекурсия в типах позволяет строить конструкции произвольной степени вложенности, а рекурсия в функциях дает возможность эти конструкции обрабатывать:

```haskell
ghci> myList = Cons 'a' (Cons 'b' (Cons 'c' Nil))
ghci> len myList
3
```
